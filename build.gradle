buildscript {
    repositories { jcenter() }
    dependencies { classpath 'io.reactivesocket:gradle-nebula-plugin-reactivesocket:1.0.6' }
}

description = 'ReactiveSocket: stream oriented messaging passing with Reactive Stream semantics.'

apply plugin: 'reactivesocket-project'

subprojects {
    apply plugin: 'reactivesocket-project'
    apply plugin: 'java'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    repositories {
        jcenter()
        maven { url 'https://oss.jfrog.org/libs-snapshot' }
        maven { url 'https://dl.bintray.com/reactivesocket/ReactiveSocket' }
    }

    dependencies {
        compile 'org.reactivestreams:reactive-streams:1.0.0.final'
        compile 'org.agrona:Agrona:0.4.13'
        compile 'io.reactivex:rxjava:latest.release'
        compile 'io.reactivex:rxjava-reactive-streams:latest.release'
        compile 'org.hdrhistogram:HdrHistogram:latest.release'
        compile 'org.slf4j:slf4j-api:latest.release'

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile "org.hamcrest:hamcrest-library:1.3"
        testRuntime 'org.slf4j:slf4j-simple:1.7.12'
    }

    test {
        testLogging.showStandardStreams = true
    }
}

// support for snapshot/final releases via versioned branch names like 1.x
nebulaRelease {
    addReleaseBranchPattern(/\d+\.\d+\.\d+/)
    addReleaseBranchPattern('HEAD')
}

if (project.hasProperty('release.useLastTag')) {
    tasks.prepare.enabled = false
}
